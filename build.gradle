plugins {
    id 'org.jetbrains.kotlin.jvm' version '2.3.0'
    id 'io.qameta.allure' version '3.0.1'
}

group = 'org.example.kotlin'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

configurations {
    agent {
        canBeResolved = true
        canBeConsumed = true
    }
}

test {
    useJUnitPlatform {
        if (System.getProperty("TAGS")) {
            includeTags System.getProperty("TAGS")
        }
    }

    dependsOn("cleanTest")
    testLogging {
        events "passed", "skipped", "failed"
    }

    //Pass system properties into JVM with tests
    systemProperties(System.getProperties())

    // URLs и браузер из gradle.properties
    systemProperty "selenide.baseUrl", findProperty("baseUrl") ?: "http://localhost:4000"
    systemProperty "api.base.url", findProperty("apiBaseUrl") ?: "http://localhost:1111"
    systemProperty "selenide.browser", findProperty("selenide.browser") ?: "chrome"
    if (hasProperty("selenide.browserVersion")) {
        systemProperty "selenide.browserVersion", findProperty("selenide.browserVersion")
    }
}

dependencies {
    // JUnit
    implementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: '6.0.2'

    // Frontend
    implementation group: 'com.codeborne', name: 'selenide', version: '7.14.0'

    // Assert
    implementation group: 'io.kotest', name: 'kotest-assertions-core', version: '6.1.1'

    // Allure
    agent "org.aspectj:aspectjweaver:1.9.22"
    implementation group: 'org.aspectj', name: 'aspectjweaver', version: '1.9.22'
    implementation group: 'io.qameta.allure', name: 'allure-junit5', version: '2.32.0'
}

sourceSets {
    test {
        kotlin.srcDirs("src/test/api", "src/test/frontend")
    }
}
